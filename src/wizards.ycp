/**
 * File:	include/profile-manager/wizards.ycp
 * Package:	Configuration of profile-manager
 * Summary:	Wizards definitions
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "profile-manager";

import "Sequencer";
import "Wizard";

include "profile-manager/complex.ycp";

/**
 * Main sequence of profile-manager
 * @return sequence result
 */
define any MainSequence () {
    map aliases =
	$[
	    "main"	:	``( MainDialog () ),
	    "resources"	:	``( ConfigureResourcesDialog() ),
	    "users"	:	``( UsersDialog() )
	];

    map sequence = $[
	"ws_start" : "main",
	"main" :
	$[
	    `abort	: `abort,
	    `cancel	: `abort,
	    `next	: `next,
	    `resources	: "resources",
	    `users	: "users",
	],
	"resources":
	$[
	    `abort	: `abort,
	    `cancel	: `abort,
	    `next	: "main",
	],
	"users":
	$[
	    `abort	: `abort,
	    `cancel	: `abort,
	    `next	: "main",
	],
    ];

    any ret = Sequencer::Run (aliases, sequence);
    return ret;
}

/**
 * Whole configuration of profile-manager
 * @return sequence result
 */
define symbol ProfileManagerSequence () {

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence () ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("profile-manager");
    any ret = Sequencer::Run (aliases, sequence);
    UI::CloseDialog();

    return (symbol) ret;
}

/* EOF */
}
