/**
 * File:	include/profile-manager/wizards.ycp
 * Package:	Configuration of profile-manager
 * Summary:	Wizards definitions
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "profile-manager";

import "Wizard";

include "ui/common_popups.ycp";
include "ui/common_messages.ycp";
include "wizard/sequencer.ycp";

include "profile-manager/complex.ycp";
include "profile-manager/dialogs.ycp";

/**
 * Whole configuration of profile-manager
 * @return sequence result
 */
global define symbol ProfileManagerSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
    "write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	]
	,"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    // Caption of the initialize process
    string caption = _("Profile-manager configuration");
    // Lable of the initialize process
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    any ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of profile-manager but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol ProfileManagerAutoSequence() ``{
    // Caption of the initialize process
    string caption = _("Profile-manager configuration");
    // Lable of the initialize process
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    BackButtonLabel(), NextButtonLabel());

    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}


global define any MainSequence () ``{
    map aliases =
	$[
	    "main"	:   ``( MainDialog () ),
	    "options"	:  ``( OptionsDialog () ),
	    "switch"	:  ``( SwitchDialog () ),
	    "edit"	:  ``( ProfileSettingsDialog () )
	];

    map sequence = $[
	"ws_start" : "main",
	"main" :
	$[
	    `abort	: `abort,
	    `next	: `next,
		`switch_button : "switch",
	    `options_button: "options",
        `delete_button: "main", // refresh table after deleting profile
	    `edit_button: "edit",
	],
	"switch" :
	$[
	    `abort	: `abort,
		`save_button : "switch", // refresh table ??
		`drop_button : "switch", // refresh table ??
		`modified : "switch", // refresh table ??
		`next	: "main"
	],
	"options" :
	$[
	    `abort	: `abort,
        `cp_button : "options",
        `del_button : "options",
	    `next	: "main"
	],
    "edit" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	]
    ];

    any ret = WizardSequencer (aliases, sequence);

    return ret;
}



/* EOF */
}
