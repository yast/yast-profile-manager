/**
 * File:	include/profile-manager/wizards.ycp
 * Package:	Configuration of profile-manager
 * Summary:	Wizards definitions
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 */

{

textdomain "profile-manager";

import "Wizard";
import "ProfileManager";

import "Popup";
import "Label";
include "wizard/sequencer.ycp";

include "profile-manager/complex.ycp";
include "profile-manager/dialogs.ycp";

/**
 * Whole configuration of profile-manager
 * @return sequence result
 */
global define symbol ProfileManagerSequence() ``{

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
    "write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	]
	,"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    any ret = WizardSequencer(aliases, sequence);
    UI::CloseDialog();

    return ret;
}

/**
 * Whole configuration of profile-manager but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
global define symbol ProfileManagerAutoSequence() ``{
    // Caption of the initialize process
    string caption = _("Profile Manager Configuration");
    // Lable of the initialize process
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    any ret = MainSequence();

    UI::CloseDialog();
    return ret;
}

/**
 * Finishing module after clicking windowmanager "close window" button
 * @return symbol next (-> write all)
 */
global define symbol Cancel() ``{

    ProfileManager::canceled = true;
    return `next; //FIXME cancel should really cancel!!
}

/**
 * Main sequence of profile-manager
 * @return sequence result
 */
global define any MainSequence () ``{
    map aliases =
	$[
	    "main"	:	``( MainDialog () ),
	    "options"	:	``( OptionsDialog () ),
	    "switch"	:	``( SwitchDialog () ),
	    "details"	:	``( DetailsSwitchDialog ()),
	    "edit"	:	``( ProfileSettingsDialog () ),
	    "cancel"	:	``( Cancel() ),
	    "resources"	:	``( ConfigureResourcesDialog() )
	];

    map sequence = $[
	"ws_start" : "main",
	"main" :
	$[
	    `abort	: `abort,
	    `cancel	: "cancel",
	    `next	: `next,
	    `switch_button : "switch",
	    `options_button: "options",
	    `edit_button: "edit",
	],
	"switch" :
	$[
	    `abort	: `abort,
	    `cancel	: "cancel",
	    `details	: "details",
	    `next	: "main"
	],
	"details" :
	$[
	    `abort	: "cancel",
	    `cancel	: "cancel",
	    `next	: "switch"
	],
	"options" :
	$[
	    `abort	: `abort,
	    `cancel	: "cancel",
	    `next	: "main",
	    `resources	: "resources"
	],
	"edit" :
	$[
	    `abort	: `abort,
	    `cancel	: "cancel",
	    `next	: "main"
	],
	"resources":
	$[
	    `abort	: "cancel",
	    `cancel	: "cancel",
	    `next	: "options"
	],
	"cancel" :
	$[
	    `next	: `next
	]
    ];

    any ret = WizardSequencer (aliases, sequence);

    return ret;
}



/* EOF */
}
