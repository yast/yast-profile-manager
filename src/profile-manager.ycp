/**
 * File:	clients/profile-manager.ycp
 * Package:	Configuration of profile-manager
 * Summary:	Main file
 * Authors:	Jiri Suchomel <jsuchome@suse.cz>
 *
 * $Id$
 *
 * Main file for profile-manager configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of the profile-manager</h3>
 */

textdomain "profile-manager";

y2milestone ("----------------------------------------");
y2milestone ("Profile-manager module started");

import "CommandLine";
import "Confirm";
import "PackageSystem";
include "profile-manager/wizards.ycp";


// --------------------------------------------------------------------------
// --------------------------------- cmd-line handlers

/**
 * Change configuration of profile-manager
 * @param options  a list of parameters passed as args
 * @return boolean anything modified?
 */
define boolean ProfileManagerChangeConfiguration (map options) {

    if (! haskey (options, "users"))
	return false;
    boolean allowed	= options["users"]:"no" == "yes";
    if (ProfileManager::users_allowed != allowed)
    {
	ProfileManager::users_allowed	= allowed;
	ProfileManager::users_modified	= true;
    }
    return ProfileManager::users_modified;
}

/**
 * Print summary of basic options
 * @return boolean false
 */
define boolean ProfileManagerSummaryHandler (map options ) {

	// summary line
    CommandLine::Print (ProfileManager::enabled ? _("SCPM is enabled.") :
	// summary line
	_("SCPM is disabled."));

    list<string> active_groups  = [];
    foreach (string name, map group, ProfileManager::GetResourceGroups (), {
	if (group["active"]:false && group["what"]:"" != "deleted")
	    active_groups = add (active_groups, name);
    });
    if (ProfileManager::enabled && size (active_groups) > 0)
    {
	// summary item
	CommandLine::Print (sformat (_("Active Resource Groups: %1"),
	    mergestring (active_groups, ",")));
    }

    return false;
}

// run the wizard sequence
define any Sequence () {

    if (!Confirm::MustBeRoot ())
	return `cancel;
    return ProfileManagerSequence ();
}


// the command line description map
map cmdline = $[
    "id"		: "profile-manager",
    // translators: command line help text for Profile manage module
    "help"		: _("Profile manager configuration module"),
    "guihandler"	: Sequence,
    "initialize"	: ProfileManager::Read,
    "finish"		: ProfileManager::Write,
    "actions"		: $[
	"summary" :$[
	    "handler"	: ProfileManagerSummaryHandler,
	    // translators: command line help text for summary action
	    "help"	: _("Configuration summary of profile manager")
	],
	"configure"	: $[
	    "handler"	: ProfileManagerChangeConfiguration,
	    // translators: command line help text for configure action
	    "help"	: _("Change the profile manager settings")
	]
    ],
    "options"		: $[
	"users"	:$[
	    // translators: command line help text for the kdc option
	    "help"	: _("Enable or disable profile switching for non-root users"),
	    "type"	: "enum",
	    "typespec"  : [ "yes", "no" ],
	],
    ],
    "mappings"		: $[
	"summary"	: [],
	"configure"	: ["users"],
    ]
];

any ret	= `auto;

if (PackageSystem::CheckAndInstallPackagesInteractive (["scpm"]))
    ret = CommandLine::Run (cmdline);

y2debug("ret == %1", ret);

y2milestone("Profile-manager module finished");
y2milestone("----------------------------------------");
return ret;
}
